buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        google()
        mavenCentral()
        jcenter()
    }
}

def getReleaseVersionCode() {
    def versionCode = (int)(new Date().getTime() / 1000)
    println('versionCode: ' + versionCode)
    return versionCode
}

def getReleaseVersionName() {
    def baseline = '1.6.7'
    def versionName = baseline + new Date().format('.MMdd-HHmm')
    println('versionName: ' + versionName)
    return versionName
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode getReleaseVersionCode()
        versionName getReleaseVersionName()
        ndk {
            abiFilters 'armeabi', 'x86', 'armeabi-v7a'
        }
    }

    signingConfigs {
        debug {
            try {
                storeFile file('debug.jks')
                storePassword "debug.dgsrz.com"
                keyAlias 'debug.github.dgsrz.com'
                keyPassword 'debug.github.dgsrz.com'
            } catch (ignored) {
                throw new InvalidUserDataException('You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.')
            }
        }
    }

    buildTypes {
        debug {
            resValue "string", "app_name", "osu!droid-debug"
            applicationIdSuffix '.debug'
        }

        debugGithubRelease {
            matchingFallbacks = ['debug']
            debuggable true
            signingConfig signingConfigs.debug
        }

        debugEdrows {
            resValue "string", "app_name", "osu!droid-ed"
            applicationIdSuffix '.debug.edrows'
            matchingFallbacks = ['debug']
            versionNameSuffix 'edrows'
            debuggable true
            proguardFile 'proguard.cfg'
        }

        release {
            resValue "string", "app_name", "osu!droid"
            zipAlignEnabled true
            minifyEnabled true
            proguardFile 'proguard.cfg'
            //signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        androidTest.setRoot('tests')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'

    lintOptions {
        abortOnError true
        ignoreWarnings true
        disable 'MissingTranslation', 'ExpiredTargetSdkVersion', 'ProtectedPermissions', 'AppLinkUrlError'
    }
}

dependencies {
    implementation project(':AndEngine')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    implementation 'com.github.EdrowsLuo:osudroidstrings:13eadc59ff'
    implementation 'com.github.EdrowsLuo.EdlGameFramework:EdlJavaExt:bbeae03503'
    implementation 'com.github.EdrowsLuo.EdlGameFramework:EdlOsbSupport:bbeae03503'
    implementation project(path: ':LibBASS')
    implementation 'androidx.cardview:cardview:1.0.0'
}
